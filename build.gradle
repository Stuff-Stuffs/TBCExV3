plugins {
    id 'fabric-loom' version '1.0-SNAPSHOT' apply false
    id 'io.github.juuxel.loom-quiltflower' version '1.7.4' apply false
    id 'maven-publish'
}

version = project.mod_version

archives_base_name = "tbcexv3"

allprojects {
    apply plugin: 'fabric-loom'
    apply plugin: 'io.github.juuxel.loom-quiltflower'
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    group = project.maven_group

    tasks.withType(JavaCompile).configureEach {
        it.options.release = 17
    }

    processResources {
        inputs.property "version", project.version

        filesMatching("fabric.mod.json") {
            expand "version": project.version
        }
    }

    loom {
        splitEnvironmentSourceSets()
    }

    sourceSets {
        testmod {
            compileClasspath += main.compileClasspath
            runtimeClasspath += main.runtimeClasspath
        }
    }

    allprojects.each { p ->
        loom.mods.register("tbcexv3_" + p.name) {
            sourceSet p.sourceSets.main
            sourceSet p.sourceSets.client
        }
    }

    repositories {

    mavenCentral()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

        // Fabric API. This is technically optional, but you probably want it anyway.
        modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    }

    java {
        withSourcesJar()
    }

    jar {
        from("LICENSE") {
            rename { "${it}_${project.archivesBaseName}" }
        }
    }
}

subprojects {
    version = project.rootProject.version
}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    implementation 'org.jetbrains.xodus:xodus-environment:2.0.1'
    include 'org.jetbrains.xodus:xodus-environment:2.0.1'

    afterEvaluate {
        subprojects.each {
            api project(path: ":${it.path}:", configuration: "namedElements")
            clientImplementation project("${it.path}:").sourceSets.client.output
            include project("${it.path}:")
        }
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
